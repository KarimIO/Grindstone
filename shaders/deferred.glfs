#version 410 core

out vec3 color; // X Y Z MaterialType

uniform sampler2D texPos;
uniform sampler2D texNormal;
uniform sampler2D texAlbedo;
uniform sampler2D texSpecular;
uniform samplerCube texRefl;

uniform vec3 eyePos;
uniform float time;

in vec2 UV;

const float pi = 3.14159;

// Schlick
vec3 Light_F(in vec3 f0, in float f90, in float VH) {
	return f0 + (f90-f0) * pow(1-VH, 5.0f);
}

// GGX
float Light_D(in float alphaSqr, in float NH) {
	float denom = NH * NH * (alphaSqr - 1) + 1;

	return alphaSqr / (pi * denom * denom);
}

float Light_V( in float NL, in float NV, in float alphaSqr ) {
	float Lambda_GGXV = NL * sqrt (( - NV * alphaSqr + NV ) * NV + alphaSqr );
	float Lambda_GGXL = NV * sqrt (( - NL * alphaSqr + NL ) * NL + alphaSqr );

	return 0.25f / ( Lambda_GGXV + Lambda_GGXL );
}

float Diff_Disney( float NdotV, float NdotL, float LdotH ,float linearRoughness ) {
	float energyBias = 0*(1-linearRoughness) + 0.5*linearRoughness;
	float energyFactor = 1.0*(1-linearRoughness) + linearRoughness / 1.51;
	float fd90 = energyBias + 2.0 * LdotH * LdotH * linearRoughness ;
	vec3 f0 = vec3(1.0f , 1.0f , 1.0f);
	float lightScatter = Light_F( f0 , fd90 , NdotL ) .r;
	float viewScatter = Light_F(f0 , fd90 , NdotV ).r;

	return lightScatter * viewScatter * energyFactor ;
}

vec3 LightPointCalc(in vec3 Albedo, in vec3 WorldPos, in vec4 Specular, in vec3 Normal, in vec3 lightPos, in float lightRadius, in vec3 lightColor, in vec3 eyePos) {
	vec3 lightDir	= WorldPos - lightPos;
	float Distance	= length(lightDir);

	vec3 eyeDir		= normalize(eyePos - WorldPos);
	vec3 eyeReflect = reflect(-eyeDir, Normal);
	
	lightDir		= -normalize(lightDir);
	
	float Roughness = Specular.a;
	float alpha = Roughness * Roughness;
	float alphaSqr = alpha*alpha;

	float NL = clamp(dot(Normal, lightDir), 0, 1);
	
	float Dist2 = Distance * Distance;
	float lrad2 = lightRadius * lightRadius;
	float factor = Dist2 / lrad2;
	float Attenuation = clamp(1 - factor * factor, 0, 1); 
	Attenuation = Attenuation*Attenuation/(Dist2+0.001);

	vec3 H = normalize(eyeDir + lightDir);
	
	float NV = abs(dot(Normal, eyeDir)) + 0.00001;
	float NH = clamp(dot(Normal, H), 0, 1);
	float LH = clamp(dot(lightDir, H), 0, 1);
	float VH = clamp(dot(eyeDir, H), 0, 1);
	
	vec3 f0 = 0.32*Specular.rgb*Specular.rgb;
	float f90 = clamp(50 * dot(f0, vec3(0.33)), 0, 1);
	
	vec3 F = Light_F(f0, f90, LH);
	float D = Light_D(alpha, NH);
	float Vis = Light_V(NL, NV, alpha);
	
	//vec3 reflColor =  texture(texRefl, eyeReflect).rgb;

	vec3 Spec = (F * D * Vis); // * reflColor;
	
	vec3 Diffuse = Albedo.rgb*vec3(Diff_Disney(NV ,NL , LH, Roughness))/pi;

	vec3 lightModifier = lightColor.xyz * Attenuation;
	vec3 BSDFValue = Diffuse + Spec;
	return NL * BSDFValue * lightModifier;
}

vec3 linearToSRGB(vec3 inColor) {
    vec3 outColor;
	outColor.r = inColor.r <= 0.0031308 ? 12.92 * inColor.r : 1.055 * pow(inColor.r, 1.0/2.4) - 0.055;
	outColor.g = inColor.g <= 0.0031308 ? 12.92 * inColor.g : 1.055 * pow(inColor.g, 1.0/2.4) - 0.055;
	outColor.b = inColor.b <= 0.0031308 ? 12.92 * inColor.b : 1.055 * pow(inColor.b, 1.0/2.4) - 0.055;
	return outColor;
}

vec3 CalculateIBL(vec3 WorldPos, vec3 Normal, vec4 Specular) {
	vec3 eyeDir		= normalize(eyePos - WorldPos.xyz);
	vec3 eyeReflect = reflect(-eyeDir, Normal);
	
	float Roughness = Specular.a;
	float alpha = Roughness * Roughness;
	
	float NV = abs(dot(Normal, eyeDir));
	
	vec3 reflectDir = normalize(eyeReflect);
	vec3 reflectPix = texture(texRefl, reflectDir).rgb;
		
	vec3 f0 = 0.32*Specular.rgb*Specular.rgb;
	float f90 = clamp(50 * dot(f0, vec3(0.33)), 0, 1);
	vec3 F = Light_F(f0, f90, NV);
	float Vis = Light_V(1, NV, alpha);
	return F * Vis * reflectPix;
}

void main() {
	vec4 Position = texture(texPos, UV);
	vec4 Normal = texture(texNormal, UV);
	vec4 Albedo = texture(texAlbedo, UV);
	vec4 Specular = texture(texSpecular, UV);

	/*vec3 lightDir = normalize(vec3(0,1,1)); //normalize(vec3(0,sin(time),cos(time)));
	float SunNL = clamp(dot(lightDir, Normal.xyz), 0, 1);
	float MoonNL = clamp(dot(-lightDir, Normal.xyz), 0, 1);
	color = vec3((SunNL+MoonNL*0.2)*0.9+0.1) * Albedo.rgb;
	color = linearToSRGB(color);
	return;*/

	float lightRadius = 24;
	float lightPow = 5;
	color = CalculateIBL(Position.xyz, Normal.xyz, Specular);
	float sideDist = 4.5;
	for (int i=-1; i <=1; i++) {
		vec3 lightPos = vec3(10,1.5,i * sideDist);
		vec3 lightColor = vec3(124, 138, 150)/16;
		color += LightPointCalc(Albedo.rgb, Position.xyz, Specular, Normal.xyz, lightPos, lightRadius, lightColor, eyePos);

		lightPos = vec3(0,1.5,i * sideDist);
		lightColor = vec3(142.5, 150, 147)/16;
		color += LightPointCalc(Albedo.rgb, Position.xyz, Specular, Normal.xyz, lightPos, lightRadius, lightColor, eyePos);

		lightPos = vec3(-10,1.5,i * sideDist);
		lightColor = vec3(150, 142, 133)/16;
		color += LightPointCalc(Albedo.rgb, Position.xyz, Specular, Normal.xyz, lightPos, lightRadius, lightColor, eyePos);
	}
	color = linearToSRGB(color);
}